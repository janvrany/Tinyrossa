changequote(`[',`]')
define([TR],[])
define([ILProp1],[])
define([ILProp2],[])
define([ILProp3],[])
define([ILChildProp],[])
define([OPCODE_NAME], [translit($1, [A-Z], [a-z])])

dnl
dnl ### TRILOpcodes pool definition ###
dnl
dnl OPCODE_MACRO(\
dnl   $1  /* .opcode               = */ aconst, \
dnl   $2  /* .name                 = */ &quot;aconst&quot;, \
dnl   $3  /* .properties1          = */ ILProp1::LoadConst, \
dnl   $4  /* .properties2          = */ ILProp2::ValueNumberShare, \
dnl   $5  /* .properties3          = */ ILProp3::LikeUse, \
dnl   $6  /* .properties4          = */ 0, \
dnl   $7  /* .dataType             = */ TR::Address, \
dnl   $8  /* .typeProperties       = */ ILTypeProp::Reference, \
dnl   $9  /* .childProperties      = */ ILChildProp::NoChildren, \
dnl   $10 /* .swapChildrenOpCode   = */ TR::BadILOp, \
dnl   $11 /* .reverseBranchOpCode  = */ TR::BadILOp, \
dnl   $12 /* .booleanCompareOpCode = */ TR::BadILOp, \
dnl   $13 /* .ifCompareOpCode      = */ TR::BadILOp, \
dnl   $14 /* .description          =    load address constant (zero value means NULL) */ \

define([OPCODE_MACRO], [OPCODE_NAME($1)])

"{ Package: 'Tinyrossa' }"

"{ NameSpace: Smalltalk }"

SharedPool subclass:#TRILOpcodes
	instanceVariableNames:''
	classVariableNames:'invalid
	include(opcodes.m4)
	'
	poolDictionaries:'TRDataTypes TRILOpcodeProps1 TRILOpcodeProps2 TRILOpcodeProps3'
	category:'Tinyrossa-IL'
!
undefine([OPCODE_MACRO])

dnl
dnl ### TRILOpcodes pool initializer ###
dnl

define([ONE_CHILD],[{ $1 }])
define([TWO_CHILD],[{ $1 . $2 }])
define([THREE_CHILD],[{ $1 . $2 . $3 }])

define([TWO_SAME_CHILD],[TWO_CHILD($1, $1)])
define([THREE_SAME_CHILD],[THREE_CHILD($1, $1, $1)])

define([INITIALIZEX_HEADER],[
pushdef([OPCODENO_LO],$2)
pushdef([OPCODENO_HI],$3)
pushdef([OPCODENO],0)
!TRILOpcodes class methodsFor:'initialization'!

initialize$1: UnspecifiedChildType
    "Automatically generated by opcodesgen. DO NOT EDIT!!"

    | NoType NoChildren Unspecified"Children" IndirectCallType BadILOp |

    NoType := Void.
    NoChildren := #().
    Unspecified"Children" := nil.
    IndirectCallType := Unspecified"Children".
    BadILOp := invalid.

])
define([INITIALIZEX_FOOTER],[
! !
popdef([OPCODENO_LO])
popdef([OPCODENO_HI])
popdef([OPCODENO])
])
define([OPCODE_MACRO],[dnl
define([OPCODENO],incr(OPCODENO))dnl Increment OPCODENO
ifelse(eval(OPCODENO_LO <= OPCODENO && OPCODENO <= OPCODENO_HI),1,OPCODE_MACRO_IMPL($1,$2,$3,$4,$5,$6,$7,$8,$9,$10,$11,$12,$13))dnl
])

define([OPCODE_MACRO_IMPL], [    "format([%03d],OPCODENO)" OPCODE_NAME($1) := TRILOpcode new.])
INITIALIZEX_HEADER(00,  1, 99)
    "000" invalid := TRILOpcode new.
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(01,100,199)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(02,200,299)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(03,300,399)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(04,400,499)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(05,500,599)
include(opcodes.m4)
INITIALIZEX_FOOTER()
undefine([OPCODE_MACRO_IMPL])

define([OPCODE_MACRO_IMPL], [    "format([%03d],OPCODENO)" OPCODE_NAME($1) initializeFrom: { 'OPCODE_NAME($1)' . $7 . $3 . $4 . $5 . $9 . $10 . $11 . $12 . $13 }.])
INITIALIZEX_HEADER(06,  1, 99)
    "000" invalid initializeFrom: { 'invalid' . NoType . 0 . 0 . 0 . Unspecified . BadILOp . BadILOp . BadILOp . BadILOp }.
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(07,100,199)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(08a,200,249)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(08b,250,299)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(09a,300,349)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(09b,350,399)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(10a,400,449)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(10b,450,499)
include(opcodes.m4)
INITIALIZEX_FOOTER()
INITIALIZEX_HEADER(11,500,599)
include(opcodes.m4)
INITIALIZEX_FOOTER()

undefine([OPCODE_MACRO])
undefine([OPCODE_MACRO_IMPL])
undefine([INITIALIZEX_HEADER])
undefine([INITIALIZEX_FOOTER])


TRILOpcodes initialize!
undefine([OPCODE_MACRO])

dnl
dnl ### TRILBuilder API ###
dnl

define([OPCODE_MACRO],[

OPCODE_NAME($1): arguments
    "Automatically generated by opcodesgen. DO NOT EDIT!!"

    ^ self build: OPCODE_NAME($1) arguments: arguments
!
])
!TRILBuilder methodsFor:'building'!
include(opcodes.m4)
!
undefine([OPCODE_MACRO])


dnl
dnl ### TRILEvaluator API ###
dnl

define([OPCODE_EVALUATOR],[[evaluate_]OPCODE_NAME($1)])
define([OPCODE_MACRO],[

OPCODE_EVALUATOR($1): node
    self subclassResponsibility.
!
])
!TRILEvaluator methodsFor:'evaluation'!
include(opcodes.m4)
!
undefine([OPCODE_MACRO])

dnl
dnl ### TRILEvaluator class initializer ###
dnl

define([OPCODE_MACRO], [    OPCODE_NAME($1) . [#]OPCODE_EVALUATOR($1): .])
!TRILEvaluator class methodsFor:'initialization'!

initialize
    "Automatically generated by opcodesgen. DO NOT EDIT!!"

    EvaluatorTable := IdentityDictionary withKeysAndValues:
    {
include(opcodes.m4)
    }.

! !

TRILEvaluator initialize!

dnl
dnl ### TRILWalker API ###
dnl
define([OPCODE_EVALUATOR],[[evaluate_]OPCODE_NAME($1)])
define([OPCODE_MACRO],[

OPCODE_EVALUATOR($1): node
    self evaluateChildren: node.
    ^ node
!
])
!TRILWalker methodsFor:'evaluation'!
include(opcodes.m4)
!
undefine([OPCODE_MACRO])
